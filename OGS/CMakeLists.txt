# Source files
set ( SOURCES
	rf.cpp
	ogs_display.h
	ogs_display.cpp
)

# Include the headers which are generated by uic and moc
# and include additional header
include_directories(
	${CMAKE_SOURCE_DIR}/Base
	${CMAKE_SOURCE_DIR}/FEM
	${CMAKE_SOURCE_DIR}/FileIO
	${CMAKE_SOURCE_DIR}/GEO
	${CMAKE_SOURCE_DIR}/MathLib
	${CMAKE_SOURCE_DIR}/MSH
)

if (LIS)
	include_directories(${LIS_INCLUDE_DIR})
endif ()

if (Shapelib_FOUND)
	include_directories(${Shapelib_INCLUDE_DIR})
endif ()


# Create the library
add_executable( ogs ${SOURCES} )

target_link_libraries( ogs
	${CMAKE_THREAD_LIBS_INIT}
	Base
	FEM
	FileIO
	GEO
	MathLib
	MSH
	BrentLib
)

if(PARALLEL_USE_MPI)
	target_link_libraries(ogs ${MPI_CXX_LIBRARIES})
endif()

if (OGS_LSOLVER STREQUAL PETSC)
	include_directories(${CMAKE_SOURCE_DIR}/MathLib/PETSC)
	target_link_libraries( ogs ${PETSC_LIBRARIES} )
endif ()

if (OGS_CHEMSOLVER STREQUAL GEMS)
	target_link_libraries( ogs
		GEM
		${Boost_LIBRARIES}
		rt
	)
endif ()

if(OGS_CHEMSOLVER STREQUAL IPQC)
	include_directories(${CMAKE_SOURCE_DIR}/IPQC/src)
	target_link_libraries(ogs iphreeqc)
endif ()

if(MKL)
	target_link_libraries(ogs ${MKL_LIBRARIES})
	 # Hack, see http://webpages.charter.net/ppluzhnikov/linker.html
	target_link_libraries(ogs ${MKL_LIBRARIES})
	if(NOT MKL_USE_STATIC)
		target_link_libraries(ogs ${CMAKE_DL_LIBS})
	endif()
endif()

if (OGS_USE_FREESTEAM)
	target_link_libraries( ogs ${FREESTEAM_LIBRARIES} )
endif ()


####################
### Installation ###
####################

install (TARGETS ogs RUNTIME DESTINATION bin COMPONENT ogs)

if(MSVC)
	set(OGS_EXE ${OGS_EXECUTABLE}.exe)
else()
	set(OGS_EXE ${OGS_EXECUTABLE})
endif()

include(GetPrerequisites)
if (EXISTS ${OGS_EXE})
	set(OGS_DEPENDENCY_PATH "/usr/local/lib;/")
	if (OGS_LSOLVER STREQUAL PETSC)
		set(OGS_DEPENDENCY_PATH "${OGS_DEPENDENCY_PATH};${PETSC_DIR}/${PETSC_ARCH}/lib")
	endif()
	message (STATUS "ogs dependency path: ${OGS_DEPENDENCY_PATH}")
	GET_PREREQUISITES(${OGS_EXE} OGS_DEPENDENCIES 1 1 "${OGS_DEPENDENCY_PATH}" "")
	message (STATUS "ogs depends on:")
	foreach(DEPENDENCY ${OGS_DEPENDENCIES})
		if(NOT ${DEPENDENCY} STREQUAL "not") # Some bug on Linux?
			message(STATUS "${DEPENDENCY}")
			GP_RESOLVE_ITEM ("/" "${DEPENDENCY}" ${OGS_EXE} "${OGS_DEPENDENCY_PATH}" DEPENDENCY_PATH)
			set (DEPENDENCY_PATHS ${DEPENDENCY_PATHS} ${DEPENDENCY_PATH})
		endif()
	endforeach ()
	install (FILES ${DEPENDENCY_PATHS} DESTINATION bin COMPONENT ogs)
endif ()
